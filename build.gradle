buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.3.0"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.3.0"
}

def api = new groovy.json.JsonSlurper().parseText(file("${api_token_path}").text)
def keystore = new groovy.json.JsonSlurper().parseText(file("${keystore_path}").text)
def mcVersion = project.mc_version

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

sourceSets.main {
    java {
        srcDirs = ["src/main/java", "dependencies/main/java"]
        exclude "stevekung/mods/stevekunglib/mixin/**"
    }
    resources {
        srcDirs = ["src/main/resources", "dependencies/main/resources"]
    }
}

version = project.version
group = "stevekung.mods.moreplanets"
archivesBaseName = "More-Planets-All"
sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "${forge_version}"
    runDir = "run"
    mappings = "${mcp_mapping}"
    makeObfSourceJar = false

    replace "@FINGERPRINT@", keystore.sha1
    replace "4.0.1.-1", project.gc_require_version
    replaceIn "MorePlanetsMod.java"
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    // Hwyla : https://tehnut.info/maven/mcp/mobius/waila/Hwyla/
    maven {
        url "http://tehnut.info/maven/"
    }
    // JEI
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://modmaven.k-4u.nl"
    }
    // Code Chicken Lib : http://chickenbones.net/maven/codechicken/CodeChickenLib/
    maven {
        url "http://chickenbones.net/maven/"
    }
    // Curse Gradle
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    // CTM : https://maven.tterrag.com/team/chisel/ctm/CTM
    maven {
        url "http://maven.tterrag.com/"
    }
    // Baubles : http://maven.thiakil.com/com/azanor/baubles/Baubles/
    maven {
        url = "http://maven.thiakil.com/"
    }
    maven {
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    // BuildCraft
    maven {
        url = "https://mod-buildcraft.com/maven/"
    }
    // Actually Additions
    maven {
        url = "https://maven.chaosfield.at/"
    }
    // Mixin
    maven {
        url = "http://repo.spongepowered.org/maven/"
    }
}

dependencies {
    compile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    compile "mekanism:Mekanism-1.12.2:9.7.5.376:api"
    compile "com.github.glitchfiend.biomesoplenty:BiomesOPlenty:1.12.2-7.0.1.2441:api"
    compile "com.mod-buildcraft:buildcraft-api:7.99.24.1"
    compile "redstone-flux:RedstoneFlux-1.12:2.1.0.6:universal"
    compile "industrial-craft:industrialcraft-2-2.8.157:ex112:api"
    compile "player-api:PlayerAPI:1.12.1:1.0"
    compile "de.ellpeck.actuallyadditions:ActuallyAdditions:1.12.2-r144:api"
    compile "appeng:appliedenergistics2:rv6-stable-6:api"
    deobfCompile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
    deobfCompile "team.chisel.ctm:CTM:MC${ctm_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
    embed("org.spongepowered:mixin:0.7.11-SNAPSHOT") { transitive = false }
}

sourceSets {
    main {
        ext.refMap = "mixins.moreplanets.refmap.json"
    }
}

curseforge {
    apiKey = api.more_planets
    project {
        id = "261990"
        changelog = "Change Log"
        changelogType = "markdown"
        releaseType = "beta"
        addGameVersion mcVersion

        mainArtifact(file("build/libs/More-Planets-" + mcVersion + "-" + version + ".jar")) {
            displayName = "More-Planets-" + mcVersion + "-" + version
            relations {
                requiredLibrary "stevekungs-lib"
                optionalLibrary "jei"
                optionalLibrary "ctm"
                optionalLibrary "hwyla"
            }
        }
    }
}

ext.finalJar = "build/libs/" + "More-Planets-" + mcVersion + "-" + version + ".jar"
ext.coreJar = "build/libs/" + archivesBaseName + "-" + version + ".jar"
ext.coreFiles = ["stevekung/mods/moreplanets/**", "assets/moreplanets/**", "pack.mcmeta", "META-INF/moreplanets_at.cfg"]

// signing jar first
task buildJar(type: SignJar, dependsOn: "processJar") {
    keyStore = keystore.keystore_path
    alias = keystore.alias
    storePass = keystore.keystore_pass
    keyPass = keystore.keystore_pass
    inputFile = project.ext.finalJar
    outputFile = project.ext.finalJar
}

// processing jar
task processJar(type: Jar, dependsOn: "rePackAllJar") {
    outputs.upToDateWhen { false }
    baseName = "More-Planets-" + mcVersion

    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
    }
    manifest {
        attributes("FMLAT": "moreplanets_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "MixinConfigs": "mixins.moreplanets.json",
                   "ForceLoadAsMod": "true"
        )
    }
}

task rePackAllJar(type: Jar, dependsOn: "copyModFiles") {
    from "temporary"
}

task copyModFiles(type: Copy, dependsOn: "reobfJar") {
    from(zipTree(project.ext.coreJar))
    into "temporary"
}

task deleteTemporary(type: Delete) {
    delete "temporary"
}

task buildDeobfJar(type: Jar) {
    from sourceSets.main.output
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    classifier = "deobf"
    manifest {
        attributes("FMLAT": "moreplanets_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "MixinConfigs": "mixins.moreplanets.json",
                   "ForceLoadAsMod": "true"
        )
    }
}

buildJar.finalizedBy(deleteTemporary)