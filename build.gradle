buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.1.0"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}

def api = new groovy.json.JsonSlurper().parseText(file("${api_token_path}").text)
def keystore = new groovy.json.JsonSlurper().parseText(file("${keystore_path}").text)
def mcVersion = project.mc_version

apply plugin: "net.minecraftforge.gradle.forge"

sourceSets.main {
    java {
        srcDirs = ["src/main/java", "dependencies/main/java"]
    }
    resources {
        srcDirs = ["src/main/resources", "dependencies/main/resources"]
    }
}

version = project.version
group = "stevekung.mods.moreplanets"
archivesBaseName = "More-Planets-All"
sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.12.2-14.23.4.2760"
    runDir = "run"
    mappings = "snapshot_20171003"
    makeObfSourceJar = false

    replace "@FINGERPRINT@", keystore.sha1
    replace "4.0.1.-1", project.gc_require_version
    replaceIn "MorePlanetsMod.java"
}

repositories {
    // Hwyla : https://tehnut.info/maven/mcp/mobius/waila/Hwyla/
    maven {
        url "http://tehnut.info/maven/"
    }
    // JEI
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    // Code Chicken Lib : http://chickenbones.net/maven/codechicken/CodeChickenLib/
    maven {
        url "http://chickenbones.net/maven/"
    }
    // Curse Gradle
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    // CTM : https://maven.tterrag.com/index.php?dir=team%2Fchisel%2Fctm%2FCTM
    maven {
        url "http://maven.tterrag.com/"
    }
}

dependencies {
    compile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    compile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
    deobfCompile "team.chisel.ctm:CTM:MC${ctm_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
    runtime "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
}

curseforge {
    apiKey = api.more_planets
    project {
        id = "261990"
        changelog = "Change Log"
        changelogType = "markdown"
        releaseType = "beta"
        addGameVersion mcVersion

        afterEvaluate { //TODO Temp fix
            gameVersionStrings.remove('Java 9')
        }

        mainArtifact(file("build/libs/More-Planets-" + mcVersion + "-" + version + ".jar")) {
            displayName = "More-Planets-" + mcVersion + "-" + version
            relations {
                requiredLibrary "stevekungs-lib"
                optionalLibrary "jei"
                optionalLibrary "ctm"
                optionalLibrary "hwyla"
            }
        }
    }
}

ext.finalJar = "build/libs/" + "More-Planets-" + mcVersion + "-" + version + ".jar"
ext.coreJar = "build/libs/" + archivesBaseName + "-" + version + ".jar"
ext.coreFiles = ["stevekung/mods/moreplanets/**", "assets/moreplanets/**", "license.txt", "pack.mcmeta", "META-INF/moreplanets_at.cfg"]

// signing jar first
task buildJar(type: SignJar, dependsOn: "processJar") {
    keyStore = keystore.keystore_path
    alias = keystore.alias
    storePass = keystore.keystore_pass
    keyPass = keystore.keystore_pass
    inputFile = project.ext.finalJar
    outputFile = project.ext.finalJar
}

// processing jar
task processJar(type: Jar, dependsOn: "rePackAllJar") {
    outputs.upToDateWhen { false }
    baseName = "More-Planets-" + mcVersion

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
    }
    manifest {
        attributes.put("FMLAT", "moreplanets_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task rePackAllJar(type: Jar, dependsOn: "copyModFiles") {
    from "temporary"
}

task copyModFiles(type: Copy, dependsOn: "reobfJar") {
    from(zipTree(project.ext.coreJar))
    into "temporary"
}

task deleteTemporary(type: Delete) {
    delete "temporary"
}

task buildDeobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = "deobf"
    manifest {
        attributes.put("FMLAT", "moreplanets_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

buildJar.finalizedBy(deleteTemporary)